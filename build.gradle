/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *  http://aws.amazon.com/apache2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

import com.github.jk1.license.render.TextReportRenderer
import com.github.jk1.license.render.SimpleHtmlReportRenderer

buildscript {
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.13.0'
    }
}

plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'com.github.jk1.dependency-license-report' version '1.17'
    id 'com.gradleup.shadow' version '8.3.5'
    id "org.openapi.generator" version "7.8.0"
}

allprojects {

    group = 'software.amazon.glue'
    version = '0.1.0'

    ext {
        scalaVersion = '2.12'
        icebergVersion = '1.6.1'
        hadoopVersion = '3.3.4'
        sparkVersion = '3.5.2'
        avroVersion = '1.11.3'
        awsV2Version = '2.27.23'
        awsV1Version = '1.12.771'

        caffeineVersion = '2.9.3'
        jacksonVersion = '2.14.2'
        http4Version = '4.5.14'
        http5Version = '5.3.1'
        log4jVersion = '2.23.1'
        slf4jVersion = '2.0.16'
        immutableVersion = '2.10.1'

        junitVersion = '5.10.1'
        mockitoVersion = '4.11.0'
        assertjVersion = '3.26.3'
        mockServerVersion = '5.15.0'
        jettyVersion = '9.4.55.v20240627'
        awaitilityVersion = '4.2.1'
        findbugVersion = '3.0.2'
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.github.jk1.dependency-license-report'

    pluginManager.withPlugin('com.diffplug.spotless') {
        spotless {
            java {
                target 'src/main/java/**/*.java', 'src/test/java/**/*.java', 'src/jmh/java/**/*.java', 'src/integration/java/**/*.java'
                // we use an older version of google-java-format that is compatible with JDK 8
                googleJavaFormat("1.7")
                removeUnusedImports()
                licenseHeaderFile "$rootDir/.baseline/copyright/copyright-header-java.txt"
            }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    test {
        useJUnitPlatform()
    }

    licenseReport {
        configurations = ['compileClasspath']
        renderers = [new SimpleHtmlReportRenderer(), new TextReportRenderer()]
    }
}